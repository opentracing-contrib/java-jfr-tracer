String compat(String src) {
	if (src.contains('.')) {
		src.substring(src.lastIndexOf('.')+1)
	} else {
		src
	}
}

// We will always need to use the appropriate Java version to build the various 
// components. This is since we want to run on JDK 7 and above. Since the JFR
// APIs changed in JDK 9, we will need to use these specific JDKs to build... 
project.afterEvaluate {
	tasks.withType(JavaCompile) {
		def version = compat(sourceCompatibility)
		def jdkHome = project.ext.has("JAVA_${version}") ? project.ext.get("JAVA_${version}") : System.getenv("JAVA_${version}")
		if (!jdkHome) {
			println "Warning: Please set path to JDK ${version} using environment variable JAVA_${version}"
			println "Falling back to current JDK"
		} else {
			options.fork = true
			options.forkOptions.javaHome = file(jdkHome)
			doFirst {
				println "$name compiles using JDK $version, $jdkHome"
			}
		}
	}
}